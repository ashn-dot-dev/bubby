namespace bubby;

import "std";

func is_whitespace(char: byte) bool {
    return char == ' ' or char == '\t' or char == '\n';
}

enum type {
    BOOL;
    INT;
    NUM;
    STR;
    VEC;
    MAP;
    SET;
}

struct value {
    var _type: type;
    var _data: union {
        var bool: bool;
        var int: s64;
        var num: f64;
        var str: []byte;
        var vec: *std::vector[[value]];
        var map: *any; # *std::hash_map[[value, value]];
        var set: *any; # *std::hash_set[[value]];
    };

    let _SELF: value = uninit;

    func init_bool(bool: bool) value {
        return (:value){
            ._type = bubby::type::BOOL,
            ._data = (:typeof(value::_SELF._data)){.bool = bool},
        };
    }

    func init_int(int: s64) value {
        return (:value){
            ._type = bubby::type::INT,
            ._data = (:typeof(value::_SELF._data)){.int = int},
        };
    }

    func init_num(num: f64) value {
        return (:value){
            ._type = bubby::type::NUM,
            ._data = (:typeof(value::_SELF._data)){.num = num},
        };
    }

    func init_str(str: []byte) value {
        var new = std::slice[[byte]]::new(countof(str));
        std::slice[[byte]]::copy(new, str);
        return (:value){
            ._type = bubby::type::STR,
            ._data = (:typeof(value::_SELF._data)){.str = new},
        };
    }

    func init_vec() value {
        var new = std::new[[std::vector[[value]]]]();
        *new = std::vector[[value]]::init();
        return (:value){
            ._type = bubby::type::VEC,
            ._data = (:typeof(value::_SELF._data)){.vec = new},
        };
    }

    func init_map() value {
        var new = std::new[[std::hash_map[[value, value]]]]();
        *new = std::hash_map[[value, value]]::init();
        return (:value){
            ._type = bubby::type::MAP,
            ._data = (:typeof(value::_SELF._data)){.map = new},
        };
    }

    func init_set() value {
        var new = std::new[[std::hash_set[[value]]]]();
        *new = std::hash_set[[value]]::init();
        return (:value){
            ._type = bubby::type::SET,
            ._data = (:typeof(value::_SELF._data)){.set = new},
        };
    }

    func parse_bool(str: []byte) std::result[[struct { var value: value; var end: usize; }, std::error]] {
        alias parse_info = struct { var value: value; var end: usize; };

        if std::str::eq(str, "true") {
            return std::result[[parse_info, std::error]]::init_value((:parse_info){
                .value = value::init_bool(true),
                .end = countof("true"),
            });
        }

        if std::str::eq(str, "false") {
            return std::result[[parse_info, std::error]]::init_value((:parse_info){
                .value = value::init_bool(false),
                .end = countof("false"),
            });
        }

        return std::result[[parse_info, std::error]]::init_error(std::error::PARSE_FAILURE);
    }

    func parse_int_or_num(str: []byte) std::result[[struct { var value: value; var end: usize; }, std::error]] {
        alias parse_info = struct { var value: value; var end: usize; };
        if countof(str) == 0 {
            return std::result[[parse_info, std::error]]::init_error(std::error::PARSE_FAILURE);
        }

        var cur = 0u;
        var type = bubby::type::INT;
        if str[cur] == '+' or str[cur] == '-' {
            cur = cur + 1;
        }
        if cur != countof(str) and not std::ascii::is_digit(str[cur]) {
            return std::result[[parse_info, std::error]]::init_error(std::error::PARSE_FAILURE);
        }
        for cur != countof(str) and (std::ascii::is_digit(str[cur]) or (type == bubby::type::INT and str[cur] == '.')) {
            if str[cur] == '.' {
                type = bubby::type::NUM;
                cur = cur + 1;
                continue;
            }
            cur = cur + 1;
        }

        if type == bubby::type::NUM {
            var result = f64::init_from_str(str[0:cur]);
            if result.is_error() {
                return std::result[[parse_info, std::error]]::init_error(result.error());
            }
            return std::result[[parse_info, std::error]]::init_value((:parse_info){
                .value = value::init_num(result.value()),
                .end = cur,
            });
        }

        var result = s64::init_from_str(str[0:cur], 10);
        if result.is_error() {
            return std::result[[parse_info, std::error]]::init_error(result.error());
        }
        return std::result[[parse_info, std::error]]::init_value((:parse_info){
            .value = value::init_int(result.value()),
            .end = cur,
        });
    }

    func parse_int(str: []byte) std::result[[struct { var value: value; var end: usize; }, std::error]] {
        alias parse_info = struct { var value: value; var end: usize; };

        var result = bubby::value::parse_int_or_num(str);
        if result.is_error() {
            return result;
        }
        var value = result.value().value;
        if value.type() != bubby::type::INT {
            value.fini();
            return std::result[[parse_info, std::error]]::init_error(std::error::PARSE_FAILURE);
        }
        return result;
    }

    func parse_num(str: []byte) std::result[[struct { var value: value; var end: usize; }, std::error]] {
        alias parse_info = struct { var value: value; var end: usize; };

        var result = bubby::value::parse_int_or_num(str);
        if result.is_error() {
            return result;
        }
        var value = result.value().value;
        if value.type() != bubby::type::NUM {
            value.fini();
            return std::result[[parse_info, std::error]]::init_error(std::error::PARSE_FAILURE);
        }
        return result;
    }

    func parse_str(str: []byte) std::result[[struct { var value: value; var end: usize; }, std::error]] {
        alias parse_info = struct { var value: value; var end: usize; };
        if countof(str) == 0 {
            return std::result[[parse_info, std::error]]::init_error(std::error::PARSE_FAILURE);
        }

        var cur = 0u;
        var buf = std::slice[[byte]]::new(0);
        if cur == countof(str) or str[cur] != '\"' {
            std::slice[[byte]]::delete(buf);
            return std::result[[parse_info, std::error]]::init_error(std::error::PARSE_FAILURE);
        }
        cur = cur + 1;
        for cur != countof(str) and str[cur] != '\"' {
            var c = str[cur];
            if c < ' ' or c > '~' {
                std::slice[[byte]]::delete(buf);
                return std::result[[parse_info, std::error]]::init_error(std::error::PARSE_FAILURE);
            }
            if c != '\\' {
                buf = std::slice[[byte]]::resize(buf, countof(buf)+1);
                buf[countof(buf)-1] = c;
                cur = cur + 1;
                continue;
            }
            if countof(str) - cur < countof("\\xXX") {
                std::slice[[byte]]::delete(buf);
                return std::result[[parse_info, std::error]]::init_error(std::error::PARSE_FAILURE);
            }
            var hex = str[cur:cur+countof("\\xXX")];
            if hex[0] != '\\' or hex[1] != 'x' {
                std::slice[[byte]]::delete(buf);
                return std::result[[parse_info, std::error]]::init_error(std::error::PARSE_FAILURE);
            }
            let DIGITS = "0123456789ABCDEF";
            var hi = std::str::find(DIGITS, (:[]byte){&hex[2], 1});
            var lo = std::str::find(DIGITS, (:[]byte){&hex[3], 1});
            if hi.is_empty() or lo.is_empty() {
                std::slice[[byte]]::delete(buf);
                return std::result[[parse_info, std::error]]::init_error(std::error::PARSE_FAILURE);
            }
            var c = (:byte)(hi.value() << 4) | (:byte)lo.value();
            buf = std::slice[[byte]]::resize(buf, countof(buf)+1);
            buf[countof(buf)-1] = c;
            cur = cur + 4;
        }
        if cur == countof(str) or str[cur] != '\"' {
            std::slice[[byte]]::delete(buf);
            return std::result[[parse_info, std::error]]::init_error(std::error::PARSE_FAILURE);
        }
        cur = cur + 1;
        return std::result[[parse_info, std::error]]::init_value((:parse_info){
            .value = (:value){
                ._type = bubby::type::STR,
                ._data = (:typeof(value::_SELF._data)){.str = buf},
            },
            .end = cur,
        });
    }

    func parse_vec(str: []byte) std::result[[struct { var value: value; var end: usize; }, std::error]] {
        alias parse_info = struct { var value: value; var end: usize; };
        if countof(str) == 0 {
            return std::result[[parse_info, std::error]]::init_error(std::error::PARSE_FAILURE);
        }

        var cur = 0u;
        var out = value::init_vec();
        if cur == countof(str) or str[cur] != '[' {
            out.fini();
            return std::result[[parse_info, std::error]]::init_error(std::error::PARSE_FAILURE);
        }
        cur = cur + 1;
        for cur != countof(str) {
            # Require space separation between elements or optional space separation before the terminating ']'.
            if out.vec().*.count() != 0 and cur != countof(str) and str[cur] != ']' and not bubby::is_whitespace(str[cur]) {
                out.fini();
                return std::result[[parse_info, std::error]]::init_error(std::error::PARSE_FAILURE);
            }
            for cur != countof(str) and bubby::is_whitespace(str[cur]) {
                cur = cur + 1;
            }
            if cur != countof(str) and str[cur] == ']' {
                break;
            }

            var result = value::parse(str[cur:countof(str)]);
            if result.is_error() {
                out.fini();
                return result;
            }
            cur = cur + result.value().end;
            out.vec().*.push(result.value().value);
        }
        if cur == countof(str) or str[cur] != ']' {
            out.fini();
            return std::result[[parse_info, std::error]]::init_error(std::error::PARSE_FAILURE);
        }
        cur = cur + 1;
        return std::result[[parse_info, std::error]]::init_value((:parse_info){
            .value = out,
            .end = cur,
        });
    }

    func parse_map(str: []byte) std::result[[struct { var value: value; var end: usize; }, std::error]] {
        alias parse_info = struct { var value: value; var end: usize; };
        if countof(str) == 0 {
            return std::result[[parse_info, std::error]]::init_error(std::error::PARSE_FAILURE);
        }

        var cur = 0u;
        var out = value::init_map();
        if cur == countof(str) or str[cur] != '{' {
            out.fini();
            return std::result[[parse_info, std::error]]::init_error(std::error::PARSE_FAILURE);
        }
        cur = cur + 1;
        for cur != countof(str) {
            # Require space separation between key-value pairs or optional space separation before the terminating '}'.
            if out.map().*.count() != 0 and cur != countof(str) and str[cur] != '}' and not bubby::is_whitespace(str[cur]) {
                out.fini();
                return std::result[[parse_info, std::error]]::init_error(std::error::PARSE_FAILURE);
            }
            for cur != countof(str) and bubby::is_whitespace(str[cur]) {
                cur = cur + 1;
            }
            if cur != countof(str) and str[cur] == '}' {
                break;
            }

            var result = value::parse(str[cur:countof(str)]);
            if result.is_error() {
                out.fini();
                return result;
            }
            cur = cur + result.value().end;
            var key = result.value().value;

            if out.map().*.contains(key) {
                out.fini();
                key.fini();
                return std::result[[parse_info, std::error]]::init_error((:std::error)&"duplicate map key");
            }

            # Require space separation between the key and value.
            if cur == countof(str) or not bubby::is_whitespace(str[cur]) {
                out.fini();
                key.fini();
                return std::result[[parse_info, std::error]]::init_error(std::error::PARSE_FAILURE);
            }
            for cur != countof(str) and bubby::is_whitespace(str[cur]) {
                cur = cur + 1;
            }
            var result = value::parse(str[cur:countof(str)]);
            if result.is_error() {
                out.fini();
                key.fini();
                return result;
            }
            cur = cur + result.value().end;
            var val = result.value().value;

            out.map().*.insert(key, val);
        }
        if cur == countof(str) or str[cur] != '}' {
            out.fini();
            return std::result[[parse_info, std::error]]::init_error(std::error::PARSE_FAILURE);
        }
        cur = cur + 1;
        return std::result[[parse_info, std::error]]::init_value((:parse_info){
            .value = out,
            .end = cur,
        });
    }

    func parse_set(str: []byte) std::result[[struct { var value: value; var end: usize; }, std::error]] {
        alias parse_info = struct { var value: value; var end: usize; };
        if countof(str) == 0 {
            return std::result[[parse_info, std::error]]::init_error(std::error::PARSE_FAILURE);
        }

        var cur = 0u;
        var out = value::init_set();
        if cur == countof(str) or str[cur] != '(' {
            out.fini();
            return std::result[[parse_info, std::error]]::init_error(std::error::PARSE_FAILURE);
        }
        cur = cur + 1;
        for cur != countof(str) {
            # Require space separation between elements or optional space separation before the terminating ')'.
            if out.set().*.count() != 0 and cur != countof(str) and str[cur] != ')' and not bubby::is_whitespace(str[cur]) {
                out.fini();
                return std::result[[parse_info, std::error]]::init_error(std::error::PARSE_FAILURE);
            }
            for cur != countof(str) and bubby::is_whitespace(str[cur]) {
                cur = cur + 1;
            }
            if cur != countof(str) and str[cur] == ')' {
                break;
            }

            var result = value::parse(str[cur:countof(str)]);
            if result.is_error() {
                out.fini();
                return result;
            }
            cur = cur + result.value().end;
            var val = result.value().value;

            if out.set().*.contains(val) {
                out.fini();
                return std::result[[parse_info, std::error]]::init_error((:std::error)&"duplicate set element");
            }

            out.set().*.insert(val);
        }
        if cur == countof(str) or str[cur] != ')' {
            out.fini();
            return std::result[[parse_info, std::error]]::init_error(std::error::PARSE_FAILURE);
        }
        cur = cur + 1;
        return std::result[[parse_info, std::error]]::init_value((:parse_info){
            .value = out,
            .end = cur,
        });
    }

    func parse(str: []byte) std::result[[struct { var value: value; var end: usize; }, std::error]] {
        alias parse_info = struct { var value: value; var end: usize; };
        if countof(str) == 0 {
            return std::result[[parse_info, std::error]]::init_error(std::error::PARSE_FAILURE);
        }

        if str[0] == 't' or str[0] == 'f' {
            return bubby::value::parse_bool(str);
        }

        if str[0] == '+' or str[0] == '-' or std::ascii::is_digit(str[0]) {
            return bubby::value::parse_int_or_num(str);
        }

        if str[0] == '\"' {
            return bubby::value::parse_str(str);
        }

        if str[0] == '[' {
            return bubby::value::parse_vec(str);
        }

        if str[0] == '{' {
            return bubby::value::parse_map(str);
        }

        if str[0] == '(' {
            return bubby::value::parse_set(str);
        }

        return std::result[[parse_info, std::error]]::init_error(std::error::PARSE_FAILURE);
    }

    func init_from_str(str: []byte) std::result[[value, std::error]] {
        var result = bubby::value::parse(str);
        if result.is_error() {
            return std::result[[value, std::error]]::init_error(result.error());
        }
        if result.value().end != countof(str) {
            return std::result[[value, std::error]]::init_error(std::error::PARSE_FAILURE);
        }
        return std::result[[value, std::error]]::init_value(result.value().value);
    }

    func fini(self: *value) void {
        switch self.*._type {
        bubby::type::BOOL or bubby::type::INT or bubby::type::NUM {
            return;
        }
        bubby::type::STR {
            std::slice[[byte]]::delete(self.*._data.str);
            return;
        }
        bubby::type::VEC {
            var iter = std::slice_iterator[[value]]::init(self.*._data.vec.*.data());
            for iter.advance() {
                value::fini(iter.current());
            }
            std::vector[[value]]::fini(self.*._data.vec);
            std::delete[[std::vector[[value]]]](self.*._data.vec);
            return;
        }
        bubby::type::MAP {
            var map = self.*.map();
            var iter = std::hash_map_iterator[[value, value]]::init(map);
            for iter.advance() {
                value::fini(iter.current().*.key);
                value::fini(iter.current().*.value);
            }
            std::hash_map[[value, value]]::fini(map);
            std::delete[[std::hash_map[[value, value]]]](map);
            return;
        }
        bubby::type::SET {
            var set = self.*.set();
            var iter = std::hash_set_iterator[[value]]::init(set);
            for iter.advance() {
                value::fini(iter.current());
            }
            std::hash_set[[value]]::fini(set);
            std::delete[[std::hash_set[[value]]]](set);
            return;
        }
        }

        std::unreachable(fileof(), lineof());
    }

    func hash(self: *value) usize {
        switch self.*._type {
        bubby::type::BOOL {
            return self.*._data.bool.hash();
        }
        bubby::type::INT {
            return self.*._data.int.hash();
        }
        bubby::type::NUM {
            return self.*._data.num.hash();
        }
        bubby::type::STR {
            return self.*._data.str.hash();
        }
        bubby::type::VEC {
            var hash = 0u;
            var iter = std::slice_iterator[[value]]::init(self.*._data.vec.*.data());
            for iter.advance() {
                hash = hash +% value::hash(iter.current());
            }
            return hash;
        }
        bubby::type::MAP {
            var map = self.*.map();
            var hash = 0u;
            var iter = std::hash_map_iterator[[value, value]]::init(map);
            for iter.advance() {
                var hash_key = iter.current().*.key.*.hash();
                var hash_val = iter.current().*.value.*.hash();
                hash = hash +% hash_key +% hash_val;
            }
            return hash;
        }
        bubby::type::SET {
            var set = self.*.set();
            var hash = 0u;
            var iter = std::hash_set_iterator[[value]]::init(set);
            for iter.advance() {
                hash = hash +% iter.current().*.hash();
            }
            return hash;
        }
        }

        std::unreachable(fileof(), lineof());
        return 0;
    }

    func eq(lhs: *value, rhs: *value) bool {
        if lhs.*._type != rhs.*._type {
            return false;
        }

        switch lhs.*._type {
        bubby::type::BOOL {
            return std::eq[[::bool]](&lhs.*._data.bool, &rhs.*._data.bool);
        }
        bubby::type::INT {
            return std::eq[[s64]](&lhs.*._data.int, &rhs.*._data.int);
        }
        bubby::type::NUM {
            return std::eq[[f64]](&lhs.*._data.num, &rhs.*._data.num);
        }
        bubby::type::STR {
            return std::eq[[[]byte]](&lhs.*._data.str, &rhs.*._data.str);
        }
        bubby::type::VEC {
            if lhs.*._data.vec.*.count() != rhs.*._data.vec.*.count() {
                return false;
            }
            var lhs_iter = std::slice_iterator[[value]]::init(lhs.*._data.vec.*.data());
            var rhs_iter = std::slice_iterator[[value]]::init(rhs.*._data.vec.*.data());
            for lhs_iter.advance() and rhs_iter.advance() {
                if not std::eq[[value]](lhs_iter.current(), rhs_iter.current()) {
                    return false;
                }
            }
            return true;
        }
        bubby::type::MAP {
            var lhs_map = lhs.*.map();
            var rhs_map = rhs.*.map();
            if lhs_map.*.count() != rhs_map.*.count() {
                return false;
            }
            var lhs_iter = std::hash_map_iterator[[value, value]]::init(lhs_map);
            for lhs_iter.advance() {
                var lhs_current = lhs_iter.current();
                var rhs_current = rhs_map.*.lookup_view(*lhs_current.*.key);
                if rhs_current.is_empty() {
                    return false;
                }
                var rhs_current = rhs_current.value();
                if not std::eq[[value]](lhs_current.*.value, rhs_current.value) {
                    return false;
                }
            }
            return true;
        }
        bubby::type::SET {
            var lhs_set = lhs.*.set();
            var rhs_set = rhs.*.set();
            if lhs_set.*.count() != rhs_set.*.count() {
                return false;
            }
            var lhs_iter = std::hash_set_iterator[[value]]::init(lhs_set);
            for lhs_iter.advance() {
                var lhs_current = lhs_iter.current();
                var rhs_current = rhs_set.*.lookup_view(*lhs_current);
                if rhs_current.is_empty() {
                    return false;
                }
                var rhs_current = rhs_current.value();
                if not std::eq[[value]](lhs_current, rhs_current) {
                    return false;
                }
            }
            return true;
        }
        }

        std::unreachable(fileof(), lineof());
        return false;
    }

    func format(self: *value, writer: std::writer, fmt: []byte) std::result[[void, std::error]] {
        if countof(fmt) != 0 {
            return std::result[[void, std::error]]::init_error(std::error::INVALID_ARGUMENT);
        }

        switch self.*._type {
        bubby::type::BOOL {
            return std::write_format(
                writer,
                "{}",
                (:[]std::formatter)[
                    std::formatter::init[[::bool]](&self.*._data.bool)]);
        }
        bubby::type::INT {
            return std::write_format(
                writer,
                "{}",
                (:[]std::formatter)[
                    std::formatter::init[[s64]](&self.*._data.int)]);
        }
        bubby::type::NUM {
            return std::write_format(
                writer,
                "{}",
                (:[]std::formatter)[
                    std::formatter::init[[f64]](&self.*._data.num)]);
        }
        bubby::type::STR {
            var result = std::write_all(writer, "\"");
            if result.is_error() {
                return result;
            }
            # All non-printable bytes, plus the double quote used to denote the
            # start and end of the string, are printed using escaped hex bytes.
            for i in countof(self.*._data.str) {
                var c = self.*._data.str[i];
                if c == '\"' or c == '\\' or c < ' ' or c > '~' {
                    let DIGITS = (:[]byte)[
                        '0', '1', '2', '3', '4', '5', '6', '7',
                        '8', '9', 'A', 'B', 'C', 'D', 'E', 'F'
                    ];
                    var hex = (:[]byte)[
                        DIGITS[(:usize)c & 0xF0 >> 4],
                        DIGITS[(:usize)c & 0x0F]
                    ];
                    var result = std::write_format(
                        writer,
                        "\\x{}",
                        (:[]std::formatter)[
                            std::formatter::init[[[]byte]](&hex)]);
                    if result.is_error() {
                        return result;
                    }
                    continue;
                }
                var result = std::write_all(writer, (:[]byte){&c, 1});
                if result.is_error() {
                    return result;
                }
            }
            return std::write_all(writer, "\"");
        }
        bubby::type::VEC {
            var result = std::write_all(writer, "[");
            if result.is_error() {
                return result;
            }
            var sep = "";
            var iter = std::slice_iterator[[value]]::init(self.*._data.vec.*.data());
            for iter.advance() {
                var result = std::write_format(
                    writer,
                    "{}{}",
                    (:[]std::formatter)[
                        std::formatter::init[[[]byte]](&sep),
                        std::formatter::init[[value]](iter.current())]);
                if result.is_error() {
                    return result;
                }
                sep = " ";
            }
            return std::write_all(writer, "]");

        }
        bubby::type::MAP {
            var map = self.*.map();
            var result = std::write_all(writer, "{");
            if result.is_error() {
                return result;
            }
            var sep = "";
            var iter = std::hash_map_iterator[[value, value]]::init(map);
            for iter.advance() {
                var result = std::write_format(
                    writer,
                    "{}{} {}",
                    (:[]std::formatter)[
                        std::formatter::init[[[]byte]](&sep),
                        std::formatter::init[[value]](iter.current().*.key),
                        std::formatter::init[[value]](iter.current().*.value)]);
                if result.is_error() {
                    return result;
                }
                sep = " ";
            }
            return std::write_all(writer, "}");
        }
        bubby::type::SET {
            var set = self.*.set();
            var result = std::write_all(writer, "(");
            if result.is_error() {
                return result;
            }
            var sep = "";
            var iter = std::hash_set_iterator[[value]]::init(set);
            for iter.advance() {
                var result = std::write_format(
                    writer,
                    "{}{}",
                    (:[]std::formatter)[
                        std::formatter::init[[[]byte]](&sep),
                        std::formatter::init[[value]](iter.current())]);
                if result.is_error() {
                    return result;
                }
                sep = " ";
            }
            return std::write_all(writer, ")");
        }
        }

        std::unreachable(fileof(), lineof());
        return std::zeroed[[std::result[[void, std::error]]]]();
    }

    func type(self: *value) bubby::type {
        return self.*._type;
    }

    func bool(self: *value) bool {
        assert self.*._type == bubby::type::BOOL;
        return self.*._data.bool;
    }

    func int(self: *value) s64 {
        assert self.*._type == bubby::type::INT;
        return self.*._data.int;
    }

    func num(self: *value) f64 {
        assert self.*._type == bubby::type::NUM;
        return self.*._data.num;
    }

    func str(self: *value) []byte {
        assert self.*._type == bubby::type::STR;
        return self.*._data.str;
    }

    func vec(self: *value) *std::vector[[value]] {
        assert self.*._type == bubby::type::VEC;
        return self.*._data.vec;
    }

    func map(self: *value) *std::hash_map[[value, value]] {
        assert self.*._type == bubby::type::MAP;
        return (:*std::hash_map[[value, value]])self.*._data.map;
    }

    func map_insert_int(self: *value, int: s64, val: bubby::value) void {
        var existing = self.*.map().*.insert(value::init_int(int), val);
        if existing.is_value() {
            var existing = existing.value();
            existing.key.fini();
            existing.value.fini();
        }
    }

    func map_lookup_int(self: *value, int: s64) std::optional[[*value]] {
        var optional = self.*.map_lookup_view_int(int);
        if optional.is_empty() {
            return std::optional[[*value]]::EMPTY;
        }
        return std::optional[[*value]]::init_value(optional.value().value);
    }

    func map_xlookup_int(self: *value, int: s64) *value {
        var optional = self.*.map_lookup_int(int);
        return optional.value();
    }

    func map_lookup_view_int(self: *value, int: s64) std::optional[[std::key_value_view[[value, value]]]] {
        var key = (:value){
            ._type = bubby::type::INT,
            ._data = (:typeof(value::_SELF._data)){.int = int},
        };
        return self.*.map().*.lookup_view(key);
    }

    func map_xlookup_view_int(self: *value, int: s64) std::key_value_view[[value, value]] {
        var optional = self.*.map_lookup_view_int(int);
        return optional.value();
    }

    func map_insert_num(self: *value, num: f64, val: bubby::value) void {
        var existing = self.*.map().*.insert(value::init_num(num), val);
        if existing.is_value() {
            var existing = existing.value();
            existing.key.fini();
            existing.value.fini();
        }
    }

    func map_lookup_num(self: *value, num: f64) std::optional[[*value]] {
        var optional = self.*.map_lookup_view_num(num);
        if optional.is_empty() {
            return std::optional[[*value]]::EMPTY;
        }
        return std::optional[[*value]]::init_value(optional.value().value);
    }

    func map_xlookup_num(self: *value, num: f64) *value {
        var optional = self.*.map_lookup_num(num);
        return optional.value();
    }

    func map_lookup_view_num(self: *value, num: f64) std::optional[[std::key_value_view[[value, value]]]] {
        var key = (:value){
            ._type = bubby::type::NUM,
            ._data = (:typeof(value::_SELF._data)){.num = num},
        };
        return self.*.map().*.lookup_view(key);
    }

    func map_xlookup_view_num(self: *value, num: f64) std::key_value_view[[value, value]] {
        var optional = self.*.map_lookup_view_num(num);
        return optional.value();
    }

    func map_insert_str(self: *value, str: []byte, val: bubby::value) void {
        var existing = self.*.map().*.insert(value::init_str(str), val);
        if existing.is_value() {
            var existing = existing.value();
            existing.key.fini();
            existing.value.fini();
        }
    }

    func map_lookup_str(self: *value, str: []byte) std::optional[[*value]] {
        var optional = self.*.map_lookup_view_str(str);
        if optional.is_empty() {
            return std::optional[[*value]]::EMPTY;
        }
        return std::optional[[*value]]::init_value(optional.value().value);
    }

    func map_xlookup_str(self: *value, str: []byte) *value {
        var optional = self.*.map_lookup_str(str);
        return optional.value();
    }

    func map_lookup_view_str(self: *value, str: []byte) std::optional[[std::key_value_view[[value, value]]]] {
        var key = (:value){
            ._type = bubby::type::STR,
            ._data = (:typeof(value::_SELF._data)){.str = str},
        };
        return self.*.map().*.lookup_view(key);
    }

    func map_xlookup_view_str(self: *value, str: []byte) std::key_value_view[[value, value]] {
        var optional = self.*.map_lookup_view_str(str);
        return optional.value();
    }

    func set(self: *value) *std::hash_set[[value]] {
        assert self.*._type == bubby::type::SET;
        return (:*std::hash_set[[value]])self.*._data.set;
    }

    func set_insert(self: *value, val: bubby::value) void {
        var existing = self.*.set().*.insert(val);
        if existing.is_value() {
            var existing = existing.value();
            existing.fini();
        }
    }
}
